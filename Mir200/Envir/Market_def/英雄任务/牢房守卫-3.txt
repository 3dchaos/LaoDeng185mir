[@main]
#IF
check [31] 1
#ACT
break
#SAY
兄弟什么时候再来陪我喝酒划拳啊..\ \
<退出/@exit>
#IF
check [30] 1
#ACT
mov D0 0
break
#SAY
兄弟又来了啊?\
给我带酒了不?\
顺便再猜两拳玩玩\
<好吧，来就来了啊/@摆龙门阵7>\
#IF
check [29] 1
checkitem 酒 1
#ACT
break
#SAY
啊，你带来啦\
<是啊，快趁热喝吧/@摆龙门阵4>
#IF
check [27] 1
#ACT
break
#SAY
好兄弟.. 这样，你帮我去城里找英奇\
告诉他铁犀让你来打酒的，他就会给你的，我在那记帐的\
那酒要乘热的好吃，你路上要抓紧点哦!\
快去帮我把酒拿来啊!\ \
<退出/@exit>
#IF
check [26] 1
#ACT
break
#SAY
兄弟. 嗝.\
啊，看兄弟这样，该是城里来的吧?\
<恩...是啊，军爷好眼力/@摆龙门阵>
#ELSESAY
嗯..英奇的桂花香酒...\
真还想能喝两口啊!\ \
<退出/@exit>
[@摆龙门阵]
哪里哪里!\
不瞒你说啊，城里来的人我都晓得!\
知道为什么，城里来的人身上都透着英奇的酒香哈!\
现在天天守着这重犯，都让我没空去灌酒了!\
<重犯!?/@摆龙门阵1>
[@摆龙门阵1]
...兄弟听过就算了也没什么事情...\
不过，兄弟，帮兄弟个小忙吧!\
兄弟没酒喝了，你帮我打壶过来过瘾吧!\
<行啊!我正好也空着，就帮你跑趟吧/@摆龙门阵2>
[@摆龙门阵2]
好兄弟.. 这样，你帮我去城里找英奇\
告诉他铁犀让你来打酒的，他就会给你的，我在那记帐的\
那酒要乘热的好吃，你路上要抓紧点哦!\
<军爷放心，包我身上了!/@摆龙门阵3>
[@摆龙门阵3]
#ACT
set [27] 1
#SAY
(寻思:看来这家伙该是守着关押的白日门门徒吧!\
趁着机会灌醉他，套点话看看!)\ \
<退出/@exit>
[@摆龙门阵4]
#ACT
TAKE 酒 1
set [30] 1
#SAY
恩，真香啊\
再来口\
<...多喝点多喝点/@摆龙门阵5>
[@摆龙门阵5]
就这么喝没兴致啊\
以前咱兄弟几个划酒令，那个热闹啊\
现在为了那几个要犯，乐子都没了\
....\
<大哥，要不咱过两手，咱来猜拳好了!/@摆龙门阵6>
[@摆龙门阵6]
#act
mov D0 0
#say
你？凑合着用吧！\
不过你小子喝不来老酒，我想想怎么玩\
... ...\
要不这样，咱来猜拳，你输局就给我50000金币买酒钱\
我要是输了给你，就自灌一大口酒，怎样?\
<行，就陪大哥你玩几把/@摆龙门阵7>
[@摆龙门阵7]
#IF
checkgold 50000
#SAY
那开始了哦!\
石头...\
剪子...\
布...\
<出石头/@出石头>  <出剪子/@出剪子>  <出布/@出布>
#ELSESAY
你身上好象不够钱哦
[@出剪子]
#IF
CheckLevelEx > 0
#ACT
mov S0 剪子
#IF
random 3
#ACT
mov S1 剪子
goto @平
break
#IF
random 2
#ACT
mov S1 石头
goto @输
#ELSEACT
mov S1 布
goto @赢
[@出石头]
#IF
CheckLevelEx > 0
#ACT
mov S0 石头
#IF
random 3
#ACT
mov S1 石头
goto @平
break
#IF
random 2
#ACT
mov S1 布
goto @输
#ELSEACT
mov S1 剪子
goto @赢
[@出布]
#IF
CheckLevelEx > 0
#ACT
mov S0 布
#IF
random 3
#ACT
mov S1 布
goto @平
break
#IF
random 2
#ACT
mov S1 剪子
goto @输
#ELSEACT
mov S1 石头
goto @赢
[@平]
#SAY
我出<$STR(S1)>，你也出<$STR(S0)>\
平了平了，再来!\
<嘿嘿，再来/@摆龙门阵7>
[@输]
#ACT
mov D0 0
TAKE 金币 50000
#SAY
我出<$STR(S1)>，你也出<$STR(S0)>\
哈哈，我赢了，我赢了\
给钱吧你\
<小小的失误，再来再来/@摆龙门阵7>
[@赢]
#ACT
inc D0 1
#SAY
我出<$STR(S1)>，你也出<$STR(S0)>\
啊，输了..\
<大哥输了，别赖帐，喝!/@摆龙门阵8>
[@摆龙门阵8]
#IF
equal D0 1
#ACT
break
#SAY
还懒你成吗?\
我喝....\
这酒就是爽口啊，哈哈\
我还想多输两局呢!\
<来来，我们继续来/@摆龙门阵7>
#IF
equal D0 2
#ACT
break
#SAY
真背啊!怎么又输了啊\
要赢你小子点酒钱还真不容易啊!\
咕...咕...咕...\
<大哥好酒量啊!/@摆龙门阵9>
#IF
large D0 2
#ACT
break
#SAY
连输三局?!我铁犀竟然连输了你三次!\
好在值班，要今天去二狗家去赌钱\
今天不亏死哈\
<大哥快喝，快喝/@摆龙门阵10>
[@摆龙门阵9]
哦哟，今个还真喝得多了点了...\
小子，你别得意啊\
老子酒量好得很呢!\
再来啊!\
<好好，大哥你站稳哦，我们再来/@摆龙门阵7>
[@摆龙门阵10]
嗝...哦哟，一下喝猛了\
不好再喝了，嗝...\
算我怕了你了，小子，改天再来\
再喝，头昏误事要掉脑袋的\
<有那么严重吗?大哥别吓人了/@摆龙门阵11>
[@摆龙门阵11]
嘿嘿..小兄弟，看你帮我送酒的份上，我告诉你个秘密\
告诉你...我看着上次刺杀国王的要犯呢!\
<啊呀，有大哥这身手保证没事的/@摆龙门阵12>
[@摆龙门阵12]
那里啊，其实我们看着是招引君入瓮!\
我们这根本没法进牢里!\
牢的入口其实在一个秘密的地方!\
而且入口还隐蔽着，有个地蛙看守着呢\
哦哟，看我，光喝酒，今天地蛙还没喂食呢!\
<大哥，我看你站都不稳了，还是我帮你去吧/@摆龙门阵13>
[@摆龙门阵13]
...这规矩是不行的，可你看我这样...\
那你悄悄的别让人给发现了\
<我这就送你去/@摆龙门阵14>
[@摆龙门阵14]
#IF
checkhum T232A 1
#ACT
break
#SAY
有人正在里面挑战，请你过会儿再来！
#IF
checkhum 0162A 1
#ACT
break
#SAY
有人正在里面挑战，请你过会儿再来！
#IF
#ACT
CLEARMAPMON T232A
Param1 T232A
Param2 20
Param3 28
MonGen 地龙 1 0
MonGen 地蛙 1 0
MAPMOVE T232A 19 22
CLEARDELAYGOTO
DELAYGOTO 300000 @返回盟重
[@返回盟重]
#IF
ISONMAP T232A
#act
mapmove 3 333 333